/*
 * Copyright 2019 Dmitry Korotych
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.github.hierynomus.license' version '0.16.1'
	id "com.github.ben-manes.versions" version "0.45.0"
	id "org.openrewrite.rewrite" version "5.35.0"
}

ext {
	set('springBootAdminVersion', "3.0.0")
}

group = 'com.github.dkorotych.heroku.spring.boot.admin'
version = "${springBootAdminVersion}-SNAPSHOT"
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'de.codecentric:spring-boot-admin-starter-server'

	rewrite("org.openrewrite.recipe:rewrite-spring:4.32.0")
}

dependencyManagement {
	imports {
		mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
	}
}

bootJar {
	archiveFileName = 'admin-console.jar'
}

license {
	header project.file('config/license-header.txt')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	ext.developers = 'Dmitry Korotych'
	strictCheck true
}

rewrite {
	activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_0")
}

task replaceVersion {
	def file = file('README.adoc')
	def content = file.text
	def versionPattern = /\d+(?:\.\d+)+/
	content = content.replaceAll(":version: ${versionPattern}", ":version: ${springBootAdminVersion}")
	file.text = content
}

task stage(dependsOn: [bootJar])

dependencyUpdates {
	revision = "release"
	gradleReleaseChannel = "release"
}
