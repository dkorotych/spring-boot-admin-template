/*
 * Copyright 2019 Dmitry Korotych
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}.RELEASE"
	}
}

plugins {
	id 'java'
	id 'net.researchgate.release' version '2.8.0'
	id 'com.github.hierynomus.license' version '0.15.0'
}

apply plugin: "org.springframework.boot"
apply plugin: 'io.spring.dependency-management'

group = 'com.github.dkorotych.heroku.spring.boot.admin'
version = "${spring_boot_admin_version}-SNAPSHOT"
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

ext {
	set('springBootAdminVersion', "${spring_boot_admin_version}")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'de.codecentric:spring-boot-admin-starter-server'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
	}
}

bootJar {
	archiveFileName = 'admin-console.jar'
}

license {
	header project.file('config/license-header.txt')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	ext.developers = 'Dmitry Korotych'
	strictCheck true
}

updateVersion.doFirst {
	def file = file('README.adoc')
	def content = file.text
	def versionPattern = /\d+(?:\.\d+)+/
	content = content.replaceAll(":version: ${versionPattern}", ":version: ${version}")
	file.text = content
}
